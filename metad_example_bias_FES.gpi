#!/usr/bin/env gnuplot

load "~/scripts/gnuplot/style/pdf.gpi"
call "~/scripts/gnuplot/style/colors.gpi" "5"

graymax=1600
graystride=100
yoffset=16

set xr [-6:7.5]
#set yr [-1.3:3]
set yr [-yoffset:16]
set output "metad_example.pdf"

set xlabel "s"
set label "V(s)" at -7.5,8 center rotate
set label "F(s) + V(s)" at -7.5,-8 center rotate

pl for [i=graystride:graymax:graystride] "< paste data/potential.data data/sum_hills/fes_".i.".dat" u 1:($2-yoffset-$4) w l lc "gray50" lw 1 notitle,\
   for [i=graystride:graymax:graystride] "data/sum_hills/fes_".i.".dat" u 1:(-$2) w l lc "gray50" lw 1 notitle,  \
   for [i in "25 200 600 1400"] "< paste data/potential.data data/sum_hills/fes_".i.".dat" u 1:($2-yoffset-$4) w l lw 2 notitle,\
   'data/potential.data' u 1:($2-yoffset) w l lw 3 lc black notitle,\
    0 lc black lw 1 dt 1 notitle
